namespace staging.vxii.basic;

/*
 * TODO
 * 
 * Inputting a concept through the UI may trigger resolution (as in the explorer currently) but
 * if resolution is disabled, may enter interactive definition of the desired
 * observation. E.g. relationships may show all targets in context for manual
 * connection, or quality may show random or AI generators. Disabled/interactive
 * resolution is a special SCENARIO that should always be available.
 */

/**
 * It's possible to not add semantics to implement
 * empty contexts. May have extents or not. May have
 * children, e.g. 2 cities + one travel connection between them.
 */
define observation testregion as {
	semantics: earth:Region
	space: {
		shape: "EPSG:4326 POLYGON((33.796 -7.086, 35.946 -7.086, 35.946 -9.41, 33.796 -9.41, 33.796 -7.086))"
		grid: 1.km
	}
	time: {
		// can have year, hour, day, month, ... all admit 'default'
		year: 2012
		// from: -- beginning
		// until: -- if present, period locator represent the beginning. Can say 'next' as
		//  a default
		step: 1.day
	}
};

// yes - this can also be put in as config, built based on user and modified
// scenarios may set the observer by giving them a conventional name (or that
// of the scenario). It's simply an observation (with constraints on the type) 
// but it is recognized as the observer of future observations when submitted.
define observer testobserver as {
   geometry: explorer
   semantics: valuation:Stakeholder society:Institution
};

project private model earth:Region
	observing
		geography:Elevation in m,
		geography:Slope in degree_angle;

// you wish
//model presence of (infrastructure:Building whose economics:MarketValue > 2000000.EUR@2015)
//	where probability of earth:Flood > 0.7;

/**
 * # Generated elevation surface
 * 
 * Fake elevation model that will produce credible DEMs using fractals, needing no data.
 */
model geography:Elevation in m
   using klab.generators.geospatial.terrain();

/**
 * # Generated slope surface
 * 
 * Fake slope model that will produce half-credible slope configurations using fractals and needing no data.
 *
 * @warning if an elevation surface is also generated, the slope profile will *not* correspond to it.
 */
model geography:Slope in degree_angle
   using klab.generators.geospatial.terrain(roughness = 0.9, detail = 9, range = 0 to 80);